import nu.studer.gradle.jooq.JooqEdition

plugins {
    id 'application'
    id 'nu.studer.jooq' version '10.2'
    id 'com.gradleup.shadow' version '9.3.1'
}

group 'net.pistonmaster'
version '1.0.0'

ext {
    slf4jVersion = '2.0.4'
    log4jVersion = '2.19.0'
}

run {
    mainClass.set('net.pistonmaster.encryptedchat.EncryptedChat')
    standardInput = System.in
}

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    // https://mvnrepository.com/artifact/io.netty/netty-all
    implementation 'io.netty:netty-all:4.2.9.Final'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.20.0'

    implementation 'com.mojang:brigadier:1.0.500'

    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    jooqGenerator 'org.xerial:sqlite-jdbc:3.51.1.0'
    implementation 'org.xerial:sqlite-jdbc:3.51.1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.2'

    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(25))
    }
}

shadowJar {
}

jar {
    manifest {
        attributes(
                'Main-Class': 'net.pistonmaster.encryptedchat.EncryptedChat'
        )
    }
}

jooq {
    version = '3.17.5'
    edition = JooqEdition.OSS
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.sqlite.JDBC'
                    url = 'jdbc:sqlite:server.sqlite'
                    user = ''
                    password = ''
                    properties {
                        property {
                            key = 'PAGE_SIZE'
                            value = 2048
                        }
                    }
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.sqlite.SQLiteDatabase'
                    }
                    generate {
                        deprecated = false
                        records = false
                        immutablePojos = false
                        fluentSetters = false
                    }
                    target {
                        packageName = 'net.pistonmaster.encryptedchat.jooq'
                        directory = 'src/generated/jooq'
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}
